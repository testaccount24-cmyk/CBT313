/* --------------------  rexx procedure  -------------------- *
 | Name:      SetZStrt                                        |
 |                                                            |
 | Function:  Add the ZSTART Profile Variable with            |
 |            the provided info if it doesn't exist           |
 |                                                            |
 | Syntax:    %setzstrt new-command                           |
 |            or                                              |
 |            x = setzstrt(new-command)                       |
 |                                                            |
 |            new-command is a new ispf command to be         |
 |            added to the ISPF ZSTART variable.              |
 |                                                            |
 | Usage Notes: The new command will be checked to see        |
 |              if it is already in the ZSTART                |
 |              variable. If found then exit, if not          |
 |              then it is added.                             |
 |                                                            |
 | Dependencies:  Must run under Applid ISR                   |
 |                                                            |
 | Return Codes: 0 - successfully added                       |
 |               4 - already in ZSTART                        |
 |               8 - no new command                           |
 |              16 - not run under ISPF                       |
 |                                                            |
 | Author:    Lionel B. Dyck                                  |
 |                                                            |
 | History:  (most recent on top)                             |
 |            10/20/16 - Creation                             |
 |                                                            |
 * ---------------------------------------------------------- */
 parse arg new_command

/* -------------------------- *
 | Check for ISPF environment |
 * -------------------------- */
 if sysvar('sysispf') /= 'ACTIVE' then do
    say 'Error. This command can ONLY be run under ISPF'
    say '       Exiting - try again.'
    Exit 16
    end

 Address ISPExec

/* --------------------------------------- *
 | Check for a new ISPF Command for ZSTART |
 * --------------------------------------- */
 if strip(new_command) = '' then do
    zedsmsg = 'Error'
    zedlmsg = 'This command requires a new ISPF command.'
    'Setmsg msg(isrz001)'
    Exit 8
    end

/* --------------------------- *
 | Get the current ISPF Applid |
 * --------------------------- */
 'Vget (zapplid)'

/* ------------------------------- *
 | If not APPLID(ISR) then recurse |
 * ------------------------------- */
 if zapplid /= 'ISR' then do
     "Select CMD(%setzstrt" new_command ") Newappl(ISR)" ,
         "Passlib"
    Exit rc
    end

/* ------------------------------------ *
 | Get the ISPF Profile Variable ZSTART |
 * ------------------------------------ */
 'Vget (zstart) Profile'

/* ----------------------------------------- *
 | If ZSTART does not exist or is empty then |
 | prime it with ISPF;                       |
 * ----------------------------------------- */
 if strip(zstart) = '' then zstart = 'ISPF;'

/* -------------------------------------------------- *
 | Test to determine if the new command is already in |
 | ZSTART and if so exit with return code of 4        |
 |                                                    |
 | Otherwise add it to the end and exit return code 0 |
 * -------------------------------------------------- */
 test = translate(new_command)
 if pos(test,translate(zstart)) > 0 then Exit 4
 else do
      zstart = strip(zstart)';'new_command
      'vput (zstart) profile'
      Exit 0
      end
