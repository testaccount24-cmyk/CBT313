  /*---------------------  rexx procedure  -------------------- *
  | Name:      REXXLAB                                         |
  |                                                            |
  | Function:  ISPF Edit Macro to Create Edit Labels for       |
  |            each routine label for REXX code.               |
  |                                                            |
  | Usage Notes: Routine labels are for routines that          |
  |              start with text and end with a :              |
  |                                                            |
  |              The label will be the 1st letter of each      |
  |              part of the routine or the 1st 5 chars of     |
  |              routine name.                                 |
  |                                                            |
  |              If the label is 5 characters, or less, then   |
  |              it will be used as is.                        |
  |                                                            |
  |              If the label with _ removed and combined      |
  |              is 5 characters, or less, then it will be     |
  |              used.                                         |
  |                                                            |
  |              Examples:                                     |
  |                  DoIt:        ->    .doit                  |
  |                  Do_It:       ->    .doit                  |
  |                  Do_It12:     ->    .doit                  |
  |                  Do_Another:  ->    .doano                 |
  |                  DoSomething: ->    .dosom                 |
  |                                                            |
  |              Routine labels with numbers or special chars  |
  |              will be have them translated to blanks.       |
  |                                                            |
  |              Existing labels will be unchanged.            |
  |                                                            |
  |              Duplicate labels will have a suffix of        |
  |              A, B, ... appended if possible.               |
  |              * if label is 5 chars then last removed and   |
  |                the suffix appended.                        |
  |                                                            |
  | Dependencies:  Designed for use with REXX code             |
  |                Requires the SAVELAB package                |
  |                                                            |
  | Author:    Lionel B. Dyck                                  |
  |                                                            |
  | History:  (most recent on top)                             |
  |            2023/08/24 LBD - Fixup labels prefixed with Z   |
  |                             by replacing Z with A          |
  |            2023/08/04 LBD - Support comment on label record|
  |            2023/08/03 LBD - Fix labels with numbers        |
  |                             e.g B2A resulted in a blank    |
  |            2023/07/06 LBD - Ignore labels starting with '" |
  |            2023/07/08 LBD - More comments and labels       |
  |            2023/07/07 LBD - Enable Edit Hiliting           |
  |            2023/06/30 LBD - fix only use : in 1st word     |
  |                             and ignore in comments         |
  |            2022/07/23 LBD - two fixes                      |
  |                             Ignore labels in a comment     |
  |                             Ignore labels in a literal     |
  |            2022/07/19 LBD - Remove any existing Compare    |
  |                             Labels (.OAxxx)                |
  |            2022/07/13 LBD - Improve label setup            |
  |            2022/07/12 LBD - Update message                 |
  |            2022/07/11 LBD - Invoke Savelab li              |
  |                           - Renamed from REXXLABL          |
  |                           - Check for LOADISPF and ignore  |
  |                             from >START to >END            |
  |            2022/07/10 LBD - Creation                       |
  |                                                            |
  * ---------------------------------------------------------- */
  Address ISREdit
  'Macro (options)'

  parse value '' with null lab. comment
  count     = 0
  current   = 0
  listart   = 0
  alpha     = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'

Start:
  '(last)  = linenum .zlast'
  call set_hilite
  call get_labels
  call check_loadispf

Do_It:
  do i = 1 to last
    if listart > 0 then do
      if i > listart
      then if i < liend
      then iterate
    end
    '(d) = line' i
    if comment /= null then
    if pos('*/',d) > 0 then
         if pos("'",d,pos('*/',d)) = 0
            then comment = null
    if pos('/*',d) > 0 then
         if pos("'",d,pos('/*',d)) = 0
         then do
      comment = 1
      if pos(':',d) > 0 then
      if pos(':',d) < pos('/*',d) then
         if pos("'",d,pos('/*',d)) = 0
         then do
        comment = 2
        if pos('*/',d) > pos('/*',d) then comment = null
      end
    end
    if comment = 1 then iterate
    if right(word(d,1),1) /= ':' then iterate
    if comment = 2 then comment = 1

    /* now filter out some records */
    d = word(d,1)
    colon = pos(':',d)
    if pos('/*',d) > 1 then
    if colon > cmt then iterate  /* ignore comments */
    quote = pos("'",d)
    if quote > 1 then
    if quote < colon then iterate  /* ignore literals */

    "(l1,l2) = Label" i
    if l1 /= null then iterate /* skip is it has a label */
    parse value d with name':'.
    if pos(left(name,1),"'"'"') > 0 then iterate
    if pos('(',name) > 0 then iterate
    name = space(translate(name,' ','0123456789$@#'),0)
    if pos('_',name) > 0 then do
      name = translate(name,' ','_')
      label = null
      if length(space(name,0)) < 6
      then label = space(name,0)
      else do w = 1 to words(name)
        label = label''left(word(name,w),1)
      end
    end
    else label = strip(left(name,5))
    label = strip(left(translate(label),5))
    if left(label,1) = 'Z' then do
       label = 'A'substr(label,2)
       end
    if lab.label = 1 then call fixup_label

    lab.label = 1
    'label' i '= .'label 0
    count = count + 1
  end

DoneWithTheTask:
  zedsmsg = null
  zedlmsg = left(count 'labels created with' current' existing.',75),
    left('Use command Savelab Li (or sl li) to list active labels.',75),
    'Use command Savelab Save (or sl save) to save active labels which can' ,
    'then be restored using the command Savelab.'
  Address ISPExec ,
    'Setmsg msg(isrz001)'
  x = savelab('LI')
  exit 1

Fixup_Label:
  Select
    When length(label) < 5 then do
      c = 0
      do forever
        c = c + 1
        tlab = label''substr(alpha,c,1)
        if lab.tlab = 1 then iterate
        label = tlab
        return
      end
    end
    Otherwise do
      label = left(label,4)   /* get 4 chars from label */
      c = 0
      do forever  /* now add suffix to it */
        c = c + 1
        tlab = label''substr(alpha,c,1)
        if lab.tlab = 1 then iterate
        label = tlab
        return
      end

    end
  end
  return

  /* ---------------------- *
  | Enable HILITE features |
  * ---------------------- */
Set_Hilite:
  'hilite off'
  'hilite logic rexx paren find search cursor'
  return

  /* --------------------------------- *
  | Get all currently assigned labels |
  * --------------------------------- */
Get_Labels:
  "Locate Label First"
  if rc > 0 then return
  do forever
    "(line) = Display_Lines"
    "(l1,l2) = Label" line
    if left(l1,3) = '.OA' then
    "RESET LABEL" line line /* remove any compare labels */
    l1 = strip(substr(l1,2))
    lab.l1 = 1
    current = current + 1
    "Locate Label Next"
    if rc > 0 then leave
  end
  return

  /* ------------------------------------------ *
  | Ignore all records within a LOADISPF block |
  * ------------------------------------------ */
Check_Loadispf:
  'Find "loadispf" first'
  if rc > 0 then do
    loadispf = 0
    return
  end
  'Find ">start" 1 first'
  '(listart) = cursor'
  'Find ">end" 1 first'
  '(liend) = cursor'
  listart = listart - 1
  liend   = liend + 1
  return
