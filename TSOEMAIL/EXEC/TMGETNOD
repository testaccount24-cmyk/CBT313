        /* ------------------------ rexx procedure -------------------------- *
         * Name:      GETNODE                                                 *
         *                                                                    *
         * Syntax:    %getnode                                                *
         *                                                                    *
         * Function:  Extract the current NJE node name from the              *
         *            JES2 SSVT.                                              *
         *                                                                    *
         *  The following code will extract the current NJE node name, also   *
         *  known as the center name from the JES2 SSVT that is pointed to    *
         *  from the SSCT.                                                    *
         *                                                                    *
         *  The current node is then placed into ISPF Variable: curnode       *
         *                                                                    *
         * Author:    Lionel B. Dyck                                          *
         *            Kaiser Permanente                                       *
         *            25 N. Via Monte                                         *
         *            Walnut Creek, CA 84598                                  *
         *            (510) 926-5332                                          *
         *                                                                    *
         * History:                                                           *
         *            04/06/98 - Add sysvar(sysjes)                           *
         *            03/10/95 - Updated with code from Mike Theys for        *
         *                       support for JES2 4.3 and cleaned up to       *
         *                       support both 3.1.3 and 4.3                   *
         *            03/23/90 - created                                      *
         *                                                                    *
         * ------------------------------------------------------------------ */

        if sysvar(systsoe) > "2040" then do            /* At v2.5 or higher? */
           curnode = sysvar(sysnode)                   /* Use builtin func.  */
           jessplv = word(sysvar('sysjes'),3)          /* Use builtin func.  */
           if substr(curnode,1,1) <> "-" then do       /* Error check        */
              Address ISPEXEC "Vput (jessplv curnode) Shared"
              return
              end
           end
        else do
        cvt    = storage(10,4)                         /* load a(cvt)        */
        dcvt   = c2d(cvt)                              /* convert to decimal */
        jesct  = storage(c2x(d2c(dcvt+x2d(128))),4)    /* addr jesct from cvt*/
        djesct = c2d(jesct)                            /* convert to decimal */
        jessct = storage(c2x(d2c(djesct+x2d(18))),4)   /* addr ssct from jsct*/
        dssct  = c2d(jessct)                           /* convert to decimal */
        ssctsue= storage(c2x(d2c(dssct+x2d(14))),4)    /* load a(ssctsuse)   */
        dsuse  = c2d(ssctsue)                          /* convert to decimal */
        ssvtuse= storage(c2x(d2c(dssct+x2d(10))),4)    /* load a(ssvt)       */
        ssvtuse = c2d(ssvtuse)                        /* convert to decimal */
        jessplv= storage(c2x(d2c(dsuse+x2d(00))),8)    /* jes2 sp level      */

        if strip(jessplv) = "SP 3.1.3" then
           curnode = storage(c2x(d2c(ssvtuse+x2d(2ac))),8) /* jes2 node */
        if strip(jessplv) = "SP 4.3.0" then
           curnode = storage(c2x(d2c(dsuse+x2d(13c))),8)  /* jes2 ownnode */
        end

        Address ISPEXEC "Vput (curnode) Shared"
